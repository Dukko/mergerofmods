xvcv_mdlc_pc_machine_auto_district_nexus = {
    base_buildtime = @xvcv_mdlc_district_time_3
    icon = xvcv_mdlc_pc_mechanical_district_nexus
    is_capped_by_modifier = no
    show_on_uncolonized = {
        exists = from
        from = {
            has_ascension_perk = xvcv_mdlc_ap_planet_scale_machines
            has_authority = auth_machine_intelligence
        }
        uses_district_set = xvcv_mdlc_pc_machine_auto_district_set
    }
    potential = {
        exists = owner
        owner = {
            has_ascension_perk = xvcv_mdlc_ap_planet_scale_machines
            has_authority = auth_machine_intelligence
        }
        uses_district_set = xvcv_mdlc_pc_machine_auto_district_set
    }
    allow = {
        NOT = {
            has_modifier = resort_colony
        }
    }
    conversion_ratio = 0.5
    convert_to = {
        xvcv_mdlc_common_district_mining
    }
    resources = {
        category = planet_districts_cities
        cost = {
            minerals = @xvcv_mdlc_district_cost_3
        }
        upkeep = {
            energy = 2
        }
    }
    planet_modifier = {
        planet_housing_add = 10
        planet_amenities_no_happiness_add = 12
        planet_max_buildings_add = 1
    }
    triggered_planet_modifier = {
        potential = {
            exists = owner
            owner = {
                has_technology = tech_housing_1
            }
        }
        modifier = {
            planet_housing_add = 1
        }
    }
    triggered_planet_modifier = {
        potential = {
            exists = owner
            owner = {
                has_technology = tech_housing_2
            }
        }
        modifier = {
            planet_housing_add = 1
            planet_amenities_no_happiness_add = 4
        }
    }
    triggered_planet_modifier = {
        potential = {
            exists = owner
            owner = {
                has_swapped_tradition = tr_prosperity_optimized_nexus
            }
        }
        modifier = {
            planet_housing_add = 1
        }
    }
}
