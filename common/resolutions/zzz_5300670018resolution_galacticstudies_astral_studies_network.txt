@resolution_weight_popular = 6.5
@resolution_cost_t2 = 150
@resolution_flag_timer = 360
resolution_galacticstudies_astral_studies_network = {
    icon = "GFX_resolution_galactic_studies"
    resources = {
        category = resolutions
        cost = {
            influence = @resolution_cost_t2
        }
    }
    target = no
    level = 2
    modifier = {
        resolution_galacticstudies_cooperative_research_channels = 1
        resolution_galacticstudies_astral_studies_network = 1
    }
    effect = {
        custom_tooltip = resolution_galacticstudies_astral_studies_network_tooltip
        hidden_effect = {
            set_timed_country_flag = {
                flag = galactic_community_resolution_passed_galacticstudies
                days = @resolution_flag_timer
            }
            add_modifier = {
                modifier = resolution_passed_diplomatic_weight
                days = 2200
            }
        }
    }
    fail_effects = {
        hidden_effect = {
            set_timed_country_flag = {
                flag = galactic_community_resolution_failed_galacticstudies
                days = @resolution_flag_timer
            }
            add_modifier = {
                modifier = resolution_failed_diplomatic_weight
                days = 2200
            }
        }
    }
    allow = {
        is_active_resolution = "resolution_galacticstudies_cooperative_research_channels"
    }
    ai_weight = {
        weight = @resolution_weight_popular
        modifier = {
            factor = 2
            not = {
                has_country_flag = str_has_bad_tech_score
            }
        }
        modifier = {
            factor = 0.8
            has_country_flag = str_has_bad_tech_score
        }
        modifier = {
            factor = 0.5
            has_country_flag = str_has_terrible_tech_score
        }
        modifier = {
            factor = 0.9
            turtler = yes
        }
        modifier = {
            factor = 0.9
            turtler2 = yes
        }
        modifier = {
            factor = 1.1
            rusher = yes
        }
        modifier = {
            factor = 1.1
            rusher2 = yes
        }
        modifier = {
            factor = 1.5
            is_materialist = yes
        }
        modifier = {
            factor = 0.75
            is_spiritualist = yes
        }
    }
}
