building_resource_silo = {
    base_buildtime = @b1_time
    category = resource
    potential = {
        NOR = {
            has_modifier = resort_colony
            has_modifier = slave_colony
        }
    }
    allow = {
        exists = owner
        owner = {
            is_ai = no
        }
        hidden_trigger = {
            OR = {
                owner = {
                    is_ai = no
                }
                AND = {
                    NOT = {
                        has_building = building_resource_silo
                    }
                    OR = {
                        owner = {
                            is_machine_empire = yes
                            has_swapped_tradition = tr_domination_modular_depot
                        }
                        is_active_resolution = "resolution_greatergood_balance_in_the_middle"
                        is_active_resolution = "resolution_greatergood_universal_prosperity_mandate"
                        owner = {
                            has_ascension_perk = ap_arcology_project
                            NOT = {
                                has_technology = tech_mega_engineering
                            }
                        }
                    }
                }
            }
        }
    }
    triggered_planet_modifier = {
        potential = {
            exists = owner
            owner = {
                is_regular_empire = yes
            }
        }
        modifier = {
            job_clerk_add = 1
        }
    }
    triggered_planet_modifier = {
        potential = {
            exists = owner
            owner = {
                is_gestalt = yes
            }
        }
        modifier = {
            job_maintenance_drone_add = 1
        }
    }
    resources = {
        category = planet_buildings
        cost = {
            minerals = 1200
        }
        upkeep = {
            energy = 1
        }
    }
    country_modifier = {
        country_resource_max_add = 8000
    }
    prerequisites = {
        "tech_basic_industry"
    }
    upgrades = {
        "eb_silo"
    }
    triggered_desc = {
        trigger = {
            exists = owner
            owner = {
                is_gestalt = yes
            }
        }
        text = job_maintenance_drone_effect_desc
    }
    triggered_desc = {
        trigger = {
            exists = owner
            owner = {
                is_gestalt = no
            }
        }
        text = job_clerk_effect_desc
    }
}
