building_plentiful_traditions_house_of_prayer = {
    base_buildtime = 360
    base_cap_amount = 1
    category = unity
    potential = {
        NOT = {
            has_modifier = resort_colony
        }
        NOT = {
            has_modifier = slave_colony
        }
        owner = {
            has_non_swapped_tradition = tr_plentiful_faith_finish
        }
    }
    allow = {
        has_major_upgraded_capital = yes
        owner = {
            NOT = {
                any_owned_planet = {
                    has_building = building_plentiful_traditions_house_of_prayer
                }
            }
            NOT = {
                any_owned_planet = {
                    has_building_construction = building_plentiful_traditions_house_of_prayer
                }
            }
        }
    }
    convert_to = {
        building_plentiful_traditions_citadel_of_motion
    }
    resources = {
        category = planet_buildings
        cost = {
            minerals = 325
            exotic_gases = 50
        }
        upkeep = {
            energy = 3
            exotic_gases = 1
        }
        produces = {
            unity = 6
        }
    }
    planet_modifier = {
        pop_happiness = 0.1
        pop_ethic_spiritualist_attraction_mult = 0.2
        planet_housing_add = 10
    }
    triggered_planet_modifier = {
        potential = {
            owner = {
                is_gestalt = no
            }
        }
        modifier = {
            job_culture_worker_add = 5
        }
    }
    triggered_planet_modifier = {
        potential = {
            owner = {
                is_hive_empire = yes
            }
        }
        modifier = {
            job_synapse_drone_add = 5
        }
    }
    triggered_planet_modifier = {
        potential = {
            owner = {
                is_machine_empire = yes
            }
        }
        modifier = {
            job_evaluator_add = 5
        }
    }
    show_tech_unlock_if = {
        owner = {
            has_non_swapped_tradition = tr_plentiful_faith_finish
        }
    }
    ai_weight = {
        weight = 12
    }
}
