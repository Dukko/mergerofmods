xeno_ward = {
    rank = 0
    clothes_texture_index = 3
    change_job_threshold = 2
    pop_modifier = {
        pop_political_power = -1
        pop_housing_usage_base = 1
        pop_amenities_usage_base = 1
    }
    triggered_planet_modifier = {
        potential = {
            has_trait = trait_robot_surveilance_protocol
        }
        planet_crime_add = -0.5
    }
    triggered_planet_modifier = {
        potential = {
            has_trait = trait_robot_poor_security
        }
        planet_crime_add = 0.5
    }
    triggered_pop_modifier = {
        potential = {
            has_trait = trait_exotic_gas_dependency
            exists = owner
            owner = {
                has_deficit = exotic_gases
            }
        }
        pop_happiness = -0.5
        planet_pops_produces_mult = -0.4
        pop_decline_speed = 2
    }
    allow_resettlement = {
        always = no
    }
    resources = {
        category = pop_category_xeno_ward
        upkeep = {
            trigger = {
                is_organic_species = yes
                is_lithoid = no
            }
            food = @living_standard_food_normal
            consumer_goods = @living_standard_luxuries_very_high
        }
        upkeep = {
            trigger = {
                has_trait = trait_exotic_gas_dependency
            }
            exotic_gases = 0.01
        }
        upkeep = {
            trigger = {
                is_organic_species = yes
                is_lithoid = yes
            }
            minerals = @living_standard_food_normal
            consumer_goods = @living_standard_luxuries_very_high
        }
        upkeep = {
            trigger = {
                is_robotic_species = yes
            }
            energy = @living_standard_energy_normal
        }
    }
}