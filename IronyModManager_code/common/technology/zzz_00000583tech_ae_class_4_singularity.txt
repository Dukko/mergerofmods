tech_ae_class_4_singularity = {
    cost = @fallentechcost
    area = physics
    tier = 7
    category = {
        field_manipulation
    }
    prerequisites = {
        "tech_precursor_design"
        "tech_dark_matter_power_core_ae"
    }
    weight = @tier5weight1
    is_rare = yes
    potential = {
        NOR = {
            has_global_flag = has_aot_mod
            has_global_flag = acot_building_forbidden
            has_global_flag = zofe_activated
            is_country_type = fallen_empire
        }
    }
    weight_modifier = {
        modifier = {
            factor = 10
            OR = {
                has_technology = tech_mine_exotic_gases
                has_technology = tech_exotic_gases
            }
        }
        modifier = {
            factor = 2
            has_tradition = tr_prosperity_adopt
        }
        modifier = {
            factor = 0
            NOT = {
                any_owned_planet = {
                    has_building = building_class_4_singularity
                }
            }
        }
        modifier = {
            factor = 0
            has_ae_capital_tech = no
        }
        modifier = {
            factor = 1.25
            research_leader = {
                area = physics
                has_trait = "leader_trait_expertise_field_manipulation"
            }
        }
    }
    ai_weight = {
        modifier = {
            factor = 1.25
            research_leader = {
                area = physics
                has_trait = "leader_trait_expertise_field_manipulation"
            }
        }
    }
}
