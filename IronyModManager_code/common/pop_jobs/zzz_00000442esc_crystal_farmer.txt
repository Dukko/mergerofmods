esc_crystal_farmer = {
    category = specialist
    condition_string = SPECIALIST_JOB_TRIGGER
    building_icon = esc_building_crystal_farm_1
    clothes_texture_index = 2
    possible_pre_triggers = {
        has_owner = yes
        is_being_purged = no
        is_being_assimilated = no
        is_sapient = yes
    }
    possible_precalc = can_fill_specialist_job
    possible = {
        hidden_trigger = {
            exists = owner
        }
        owner = {
            is_gestalt = no
        }
        complex_specialist_job_check_trigger = yes
    }
    resources = {
        category = planet_jobs
        produces = {
            rare_crystals = @ESC_job_primary_resource_base_production
        }
        produces = {
            trigger = {
                exists = owner
                owner = {
                    is_galactic_community_member = yes
                }
                ESC_TRIGGER_environment_ordinance_waivers = yes
            }
            rare_crystals = @ESC_job_primary_resource_cornucopia_bonus
        }
        upkeep = {
            minerals = @ESC_job_upkeep_primary_resource
            consumer_goods = @ESC_job_upkeep_tertiary_resource
        }
        upkeep = {
            trigger = {
                planet = {
                    colony_type = col_refinery
                }
            }
            minerals = @ESC_job_upkeep_primary_resource_discount
            consumer_goods = @ESC_job_upkeep_tertiary_resource_discount
        }
        upkeep = {
            trigger = {
                planet = {
                    colony_type = col_habitat_refinery
                }
            }
            minerals = @ESC_job_upkeep_primary_resource_discount_hab
            consumer_goods = @ESC_job_upkeep_tertiary_resource_discount_hab
        }
    }
    weight = {
        weight = @specialist_job_weight
        modifier = {
            factor = 0.2
            has_citizenship_rights = no
        }
        modifier = {
            factor = 1.5
            OR = {
                has_trait = trait_strong
                has_trait = trait_very_strong
                has_trait = trait_agrarian
                has_trait = trait_industrious
                has_trait = trait_cybernetic
            }
        }
        modifier = {
            factor = 0.75
            OR = {
                has_trait = trait_nerve_stapled
                has_trait = trait_weak
            }
        }
        modifier = {
            factor = 5
            has_job = esc_crystal_farmer
        }
    }
}
